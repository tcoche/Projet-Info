% Explication du contenu
%
% ce code contient 2 grandes fonctions :
% 1. PartitionToTimedList
% cette fonction permet de transformer une partition musicale en 
% une liste de notes et d'accords, chacun associe a une duree.
% 
% 2. Mix
% permet de mixer plusieurs formats d'entree (dont les partitions, les 
% listes de notes/accord chronometres, et des donnees audio brutes)
% afin d'emetrre un fichier audio WAV.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


% 1. PartitionToTimedList
% contient des sous fonctions divise en 6 parties


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PARTIE 1 : Extend des notes et chords.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Extend un note.

declare
fun {NoteToExtended Note}
      case Note
      of Name#Octave then
         note(name:Name octave:Octave sharp:true duration:1.0 instrument:none)
      [] Atom then
         case {AtomToString Atom}
         of [_] then
            note(name:Atom octave:4 sharp:false duration:1.0 instrument:none)
         [] [N O] then
            note(name:{StringToAtom [N]}
                 octave:{StringToInt [O]}
                 sharp:false
                 duration:1.0
                 instrument: none)
	 else silence(duration : 1.0)
       	 end
      end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Extend un chord.

declare
fun {ChordToExtended Chord}
   case Chord
   of nil then nil
   [] H|T then {NoteToExtended H}|{ChordToExtended T}
   end
end
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PARTIE 2 : check le type de <partition item> 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Check si <partition item> est un extended note.

declare
fun{IsExtendedNote PartItem}
   case PartItem of note(name:V octave:W sharp:X duration:Y instrument:Z)
   then true
   []silence(duration:D)
   then true
   else false
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Check si <partition item> est un extended chord.

declare
fun{IsExtendedChord PartItem}

   case PartItem of H|T then      % check si c'est une liste
      {IsExtendedNote H}          % check si le 1er element est un extended note
   []nil then true
   else false                                
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Check si <partition item> est un note.

declare
fun{IsNote PartItem}

   case PartItem of Name#Octave
   then true
   else {IsAtom PartItem}
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PARTIE 3 : 
% La transformation : duration(seconds:S Part)

% Ce record souhaite executer duration sur la partition Part.
% Cette tansformation fixe la duree de la partition au nombre de secondes S.
% Il faut donc adapter la duree de chaque note et accord proportionnellement a leur
% duree actuelle pour que la duree totale devienne celle indiquee dans la transformation, S.

% Il nous faut :
% 1. la duree totale de la partition Part.
% 2. pouvoir changer la duree de chaque note et accord.
% 3. mettre ensemble 1 et 2 dans une fonction intermediaire qui etire une partition. 
% 4. mettre ensemble 1,2 et 3 dans une fonction principale.

%% IMPORTANT
% Dans la partition Part, sur laquelle on execute la transformation, 
% il est possible qu'elle contienne elle meme une transformation.
% Il faut donc d'abord gerer la transformation interne avant d'appliquer la
% transformation souhaitee.
% Pour resoudre cela, chaque fonction s'occupera d'abord de transformer sa partition en flat partition.
% On peut donc considere que toutes les partitions avec lesquelles les sous fonctions travaillent
% sont des flat partition.
% Ceci vaut de meme pour les autres transformations.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
% 1. calcule la duree totale de la partition FlatPart
% en additionant la duree de chaque note et chaque accord.
% renvoie le FLOAT Acc
% Note : la partition doit etre FLAT

declare
fun{Duration FlatPart}
   local fun{Duration1 FlatPart Acc}
	    case FlatPart of H|T
	    then
	       case H of A|B
	       then {Duration1 T (Acc + A.duration)}
	       else {Duration1 T (Acc + H.duration)}
	       end
	    []nil then Acc
	    end
	 end
   in
      {Duration1 FlatPart 0.0}
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%   
% 2. Change la duree d'un extended chord Chord.
% Note : Chord doit etre un extended chord
% Note : NewTimeFactor doit etre FLOAT 

declare
fun{DurationChangeChord Chord NewTimeFactor}

   case Chord of H|T
   then
      case H of note(name:N octave:O sharp:S duration:D instrument:I)
      then note(name:N octave:O sharp:S duration:(NewTimeFactor*D) instrument:I)|{DurationChangeChord T NewTimeFactor}
      []silence(duration:D)
      then silence(duration:(NewTimeFactor*D))|{DurationChangeChord T NewTimeFactor}
      end
   []nil then nil
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 3. Etire la duree de la partition FlatPart par le facteur NewTimeF.
% Note : La partition doit etre flat
% Note : NewTimeF doit etre FLOAT

declare
fun{DurationChangeFactor NewTimeF FlatPart}
   case FlatPart of H|T
   then
      case H of note(name:N octave:O sharp:S duration:D instrument:I)
      then note(name:N octave:O sharp:S duration:(NewTimeF*D) instrument:I)|{DurationChangeFactor NewTimeF T}
      []silence(duration:D)
      then silence(duration:(NewTimeF*D))|{DurationChangeFactor NewTimeF T}
      else {DurationChangeChord H NewTimeF}|{DurationChangeFactor NewTimeF T}
      end
   else nil
   end	  
end


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% cette fonction utilise PartitionToTimedList donc elle doit se trouver a l'interieur de celui ci
% declarer localement

% 4. Fixe la duree de la partition Part au nombre de secondes NewTime.
% Note : NewTime = FLOAT

%fun{DurationChange NewTime Part}
%	    local FlatPart in                        % Toutes les transformations doivent renvoyer une <flat partition>
%	       FlatPart = {PartitionToTimedList Part}
%	       local Time in                            % duree de la partition
%		  Time = {Duration FlatPart}
%		  local NewTimeF in                      % facteur par lequel il faut multiplier tout duration
%		     NewTimeF = (NewTime/Time)
%		     {DurationChangeFactor NewTimeF FlatPart}
%		  end
%	       end
%	    end
%end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   
% PARTIE 4 : 
% La transformation : stretch(factor:F Part)

% Etire la duree de partition par le facteur F 
% On va donc utiliser DurationChangeFactor
   
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% cette fonction utilise PartitionToTimedList donc elle doit se trouver a l'interieur de celui ci
% declarer localement

%fun{Stretch Factor Partition}
%	    local FlatPart in
%	       FlatPart = {PartitionToTimedList Partition}
%
%	       {DurationChangeFactor Factor FlatPart}
%	    end
%end
   
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PARTIE 5 : 
% La transformation : drone(note:<note or chord> amount:<natural>)

% Repeter le note/chord autant de fois que indiquee par amount 
% Drone est la seule transformation qui ne contient pas de partition a transformer mais
% un element de partition (peut contenir tous les elements sauf une autre transformation)
% Drone doit toujours renvoyer une flat partition mais cette fois ci sans l'aide de PartitionToTimedList

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Repeter le Sound autant de fois que indiquee par Amount 
% Renvoie une flat partition 

declare
fun{Drone Sound Amount}
   local S in
      if {IsList Sound}
      then if (Sound == nil)
	   then S = silence(duration:0.0)
	   elseif{IsExtendedChord Sound}
	   then S = Sound
	   else S = {ChordToExtended Sound}
	   end
      elseif {IsExtendedNote Sound}
      then S = Sound
      else S = {NoteToExtended Sound}
      end

      local fun{Duplicate S Amount}
	       if (Amount==0) then nil
	       else S|{Duplicate S (Amount-1)}
	       end
	    end
      in
	 {Duplicate S Amount}
      end
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PARTIE 6 : transpose(semitones:<integer> Part)

% Procedure de creation pour
% fun{Transpose X Part}
% Transposer une partition de X demi-tons vers le heut ou vers le bas
% fun{Transpose X ExtNote}
% Transposer un extended note de plusieurs semi-tons

% on va associer chaque note a un chiffre
% L = [c c# d d# e f f# g g# a a# b]
% devient
% L = [1 2 3 4 5 6 7 8 9 10 11 12]

% de cette facon on sait facilement utiliser le nombre donne en argument: X

% 1. on cree une fonction qui convertit une note --> chiffre
% 2. l'inverse donc un chiffre --> note
% 3. on change une note sous forme de int
% 4. on change une note sous forme extended note
% Ensuite
% 5. on mets tout ensemble dans la fonction fun{Transpose X Part}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 1. N est extended note et on veut l'attribuer a un chiffre entre 1 et 12
%%%%ATTENTION on ne tient pas compte de l'octave

declare
fun{NoteToInt N}
   case N.name of c then
      if N.sharp then 2
      else 1
      end
   []d then
      if N.sharp
      then 4
      else 3
      end
   []e then 5
   []f then
      if N.sharp
      then 7
      else 6
      end
   []g then
      if N.sharp
      then 9
      else 8
      end
   []a then
      if N.sharp
      then 11
      else 10
      end
   else 12
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 2. on traduit le int en un extended note 
%%% ATTENTION on ne fais pas guaffe a l'octave, il faut le changer par apres ET AUSSI la duration est mise a 1.0

declare
fun{IntToNote I}

   local L in
   L = [c c#4 d d#4 e f f#4 g g#4 a a#4 b]
      local ChordNote
      in ChordNote = {ChordToExtended L}

      {Nth ChordNote I}

      end
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% 3. a function that changes the integer representing the note to the correct one
% while returning also the correct octave change

declare
fun{ChangeInt X NoteInt}
   local Result1 Result2 in
      if (X >= 0)
      then Result1 = NoteInt + X
	 local fun{CorrectResultP Result Acc}
		  if (Result > 12)
		  then {CorrectResultP (Result-12) (Acc+1)}
		  else result(int:Result octave:Acc)
		  end
	       end
	 in
	    Result2 = {CorrectResultP Result1 0}
	 end
	 Result2

      else
	 Result1 = NoteInt + X
	 local fun{CorrectResultN Result Acc}
		  if (Result =< 0)
		  then {CorrectResultN (Result+12) (Acc-1)}
		  else result(int:Result octave:Acc)
		  end
	       end
	 in
	    Result2 = {CorrectResultN Result1 0}
	 end
	 Result2
      end
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 4. 
% on considere que Note est extended

declare
fun{ChangeNote X Note}
% on considere que Note est extended
   case Note of silence(duration:D)
   then silence(duration:D)
   else
      local NoteInt in
	 NoteInt = {NoteToInt Note}
	 local Result in
	    Result = {ChangeInt X NoteInt}  % renvoi result(int octave)
	    local NewNote in
	       NewNote = {IntToNote Result.int}
	       case NewNote of note(name:N octave:O sharp:S duration:D instrument:I)
	       then note(name:N octave:(Note.octave + Result.octave) sharp:S duration:(Note.duration) instrument:I)
	       else false
	       end
	    end
	 end
      end
   end
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%5.
% on doit inserer ceci dans P2T

%fun{Transpose X Part}
%	    local FlatPart in
%	       FlatPart = {PartitionToTimedList Part}
%  
%	       local fun{ChangeChord X Chord}
%			case Chord of H|T
%			then {ChangeNote X H}|{ChangeChord X T}
%			[]nil then nil
%			end
%		     end
%	       in
%		  case FlatPart of H|T 
%		  then
%		     if {IsList H}
%		     then {ChangeChord X H}|{Transpose X T}
%		     else {ChangeNote X H}|{Transpose X T}
%		     end
%		  []nil then nil
%		  end
%	       end
%	    end
% end




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% explication GENERALE
%
%
%
%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



declare
fun {PartitionToTimedList Partition}
   local
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      
      % NewTime doit etre un FLOAT

      fun{DurationChange NewTime Part}
	 local FlatPart in                        % Toutes les transformations doivent renvoyer une <flat partition>
	    FlatPart = {PartitionToTimedList Part}
	    local Time in                            % duree de la partition
	       Time = {Duration FlatPart}
	       local NewTimeF in                      % facteur par lequel il faut multiplier tout duration
		  NewTimeF = (NewTime/Time)
		  {DurationChangeFactor NewTimeF FlatPart}
	       end
	    end
	 end
      end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      fun{Stretch Factor Partition}
	 local FlatPart in
	    FlatPart = {PartitionToTimedList Partition}

	    {DurationChangeFactor Factor FlatPart}
	 end
      end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      fun{Transpose X Part}
	 local FlatPart in
	    FlatPart = {PartitionToTimedList Part}
   
	    local fun{ChangeChord X Chord}
		     case Chord of H|T
		     then {ChangeNote X H}|{ChangeChord X T}
		     []nil then nil
		     end
		  end
	    in
	       case FlatPart of H|T 
	       then
		  if {IsList H}
		  then {ChangeChord X H}|{Transpose X T}
		  else {ChangeNote X H}|{Transpose X T}
		  end
	       []nil then nil
	       end
	    end
	 end
      end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
   in

      case Partition of H|T
      then
	 if {IsList H}
	 then
	    if (H==nil)
	    then silence(duration:0.0)|{PartitionToTimedList T}		  
	    elseif {IsExtendedChord H}
	    then H|{PartitionToTimedList T}
	    else {ChordToExtended H}|{PartitionToTimedList T}
	    end

	 elseif {IsNote H}
	 then {NoteToExtended H}|{PartitionToTimedList T}
      
	 elseif {IsExtendedNote H}
	 then H|{PartitionToTimedList T}

	 else
	    case H of duration(seconds:X Part)
	    then {Append {DurationChange X Part} {PartitionToTimedList T}}
	    []stretch(factor:Factor Part)
	    then {Append {Stretch Factor Part} {PartitionToTimedList T}}
	    []drone(note:Sound amount:Amount)
	    then {Append {Drone Sound Amount} {PartitionToTimedList T}}
	    []transpose(semitones:X Part)
	    then {Append {Transpose X Part} {PartitionToTimedList T}}
	    else false
	    end
	 end
      []nil then nil
      else false
      end
   end
end







% TA PARTIE bisous
